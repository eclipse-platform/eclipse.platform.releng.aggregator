pipeline {
	options {
		skipDefaultCheckout()
		timestamps()
		timeout(time: 15, unit: 'MINUTES')
		buildDiscarder(logRotator(numToKeepStr:'5'))
	}
	agent {
		label 'basic'
	}
	stages {
		stage('Process Input') {
			steps {
				script {
					if (!params.releaseBuildID) {
						error "Required parameter 'releaseBuildID' is not defined."
					}
					env.RELEASE_BUILD_ID = params.releaseBuildID.trim()
					def releaseIDMatcher = env.RELEASE_BUILD_ID =~ /(?<type>[SR])-(?<major>\d+)\.(?<minor>\d+)(\.\d+)?((M|RC)\d+[a-z]?)?-\d{12}/
					if (!releaseIDMatcher.matches()) {
						error "releaseID: ${RELEASE_BUILD_ID}, does not match the expected pattern."
					}
					env.RELEASE_TYPE = releaseIDMatcher.group('type')
					env.RELEASE_VERSION_MAJOR = releaseIDMatcher.group('major')
					env.RELEASE_VERSION_MINOR = releaseIDMatcher.group('minor')
					dropIDMatcher = null // release matcher as it's not serializable
				}
				sh '''
					echo 'Input parameters read successfully'
					echo "RELEASE_BUILD_ID='$RELEASE_BUILD_ID'"
					echo "RELEASE_TYPE='$RELEASE_TYPE'"
					echo "RELEASE_VERSION_MAJOR='$RELEASE_VERSION_MAJOR'"
					echo "RELEASE_VERSION_MINOR='$RELEASE_VERSION_MINOR'"
				'''
			}
		}
		stage('Make Download Page visible') {
			steps {
				sshagent(['projects-storage.eclipse.org-bot-ssh']) {
					sh '''#!/bin/bash -xe
						# Build machine locations (would very seldom change)
						DOWNLOAD_ROOT=${DOWNLOAD_ROOT:-/home/data/httpd/download.eclipse.org}
						ARCHIVE_ROOT=${ARCHIVE_ROOT:-/home/data/httpd/archive.eclipse.org} 
						ROOT_LOCATIONS=("${DOWNLOAD_ROOT}")
						if [[ "${RELEASE_TYPE}" == 'R' ]]; then
							ROOT_LOCATIONS+=("${ARCHIVE_ROOT}")
						fi
						MARKER_FILES=("eclipse/downloads/drops4/${RELEASE_BUILD_ID}/buildHidden" "equinox/drops/${RELEASE_BUILD_ID}/buildHidden")
						
						for rootLocation in "${ROOT_LOCATIONS[@]}"; do
							for markerFile in "${MARKER_FILES[@]}"; do
								ssh genie.releng@projects-storage.eclipse.org rm --force "${rootLocation}/${markerFile}"
							done
						done
					'''
				}
				build job: 'Releng/updateIndex', wait: false
			}
		}
		stage('Make Release Repository visible') {
			when {
				environment name: 'RELEASE_TYPE', value: 'R'
			}
			steps {
				build job: 'Releng/modifyP2CompositeRepository', wait: true, propagate: true, parameters: [
					string(name: 'repositoryPath', value: "eclipse/updates/${RELEASE_VERSION_MAJOR}.${RELEASE_VERSION_MINOR}"),
					string(name: 'add', value: "${RELEASE_BUILD_ID}")
				]
			}
		}
	}
}
