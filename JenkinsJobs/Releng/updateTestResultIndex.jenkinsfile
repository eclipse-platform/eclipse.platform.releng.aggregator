// Constants read by job creation
private static final _JOB_DESCRIPTION = 'Performs some summary analysis and writes unit test results to the download page.'

pipeline {
	options {
		skipDefaultCheckout()
		timestamps()
		timeout(time: 30, unit: 'MINUTES')
		buildDiscarder(logRotator(numToKeepStr:'15'))
		disableConcurrentBuilds()
	}
	parameters {
		string(name: 'buildID', trim: true, description: 'ID of the I-build being tested.')
	}
	agent {
		label 'basic'
	}
	tools {
		jdk 'temurin-jdk21-latest'
	}
	stages {
		stage('Checkout SCM') {
			steps {
				dir("${WORKSPACE}/git-repo") {
					checkout scmGit(userRemoteConfigs: [[url: "${scm.userRemoteConfigs[0].url}"]], branches: [[name: "${scm.branches[0].name}"]],
						extensions: [cloneOption(depth: 1, shallow: true, noTags: true), sparseCheckout([
						[path: 'cje-production/'],
						[path: 'eclipse.platform.releng.tychoeclipsebuilder/eclipse/publishingFiles/testManifest.xml'],
						[path: 'JenkinsJobs/shared/utilities.groovy'],
					])])
				}
			}
		}
		stage('Update test results'){
			environment {
				// Download Server locations (seldomly change)
				EP_ECLIPSE_DROPS = '/home/data/httpd/download.eclipse.org/eclipse/downloads/drops4'
				ECLIPSE = installLatestEclipse()
			}
			steps {
				sshagent(['projects-storage.eclipse.org-bot-ssh']) {
					sh '''#!/bin/bash -xe
						
						curl -L -o "${WORKSPACE}/buildproperties.shsource" http://download.eclipse.org/eclipse/downloads/drops4/${buildID}/buildproperties.shsource
						source "${WORKSPACE}/buildproperties.shsource"
						
						$ECLIPSE -data workspace-toolsinstall \
							-application org.eclipse.equinox.p2.director \
							-repository ${ECLIPSE_RUN_REPO},${BUILDTOOLS_REPO} \
							-installIU org.eclipse.releng.build.tools.feature.feature.group
						
						buildDirectory="${WORKSPACE}/postingDir/${buildID}"
						testResultsDir="${buildDirectory}/testresults"
						mkdir -p "${testResultsDir}"
						
						# Fetch previously collected test results of all completed runs
						rsync -avzh genie.releng@projects-storage.eclipse.org:${EP_ECLIPSE_DROPS}/${buildID}/testresults/xml ${testResultsDir}
						
						#triggering ant runner
						$ECLIPSE -debug -data ${WORKSPACE}/workspace-updateTestResults \
							-application org.eclipse.ant.core.antRunner \
							-file "${WORKSPACE}/git-repo/cje-production/scripts/publish.xml" \
							-DbuildDirectory=${buildDirectory} \
							-DbuildType=${BUILD_TYPE} \
							"-DtestsConfigExpected=${TEST_CONFIGURATIONS_EXPECTED}" \
							"-DmanifestFile=${WORKSPACE}/git-repo/eclipse.platform.releng.tychoeclipsebuilder/eclipse/publishingFiles/testManifest.xml"
						
						rsync -avzh ${buildDirectory} genie.releng@projects-storage.eclipse.org:${EP_ECLIPSE_DROPS}
					'''
				}
				build job: 'Releng/updateIndex', wait: false
			}
		}
	}
}

def installLatestEclipse(){
	def props = readProperties(file: "${WORKSPACE}/git-repo/cje-production/buildproperties.txt").collectEntries{n, v ->
			v = v.trim();
			return [n, (v.startsWith('"') && v.endsWith('"') ? v.substring(1, v.length() - 1) : v)]
		}
	def utilities = load "${WORKSPACE}/git-repo/JenkinsJobs/shared/utilities.groovy"
	def eclipseURL = "https://download.eclipse.org/eclipse/downloads/drops4/${props.PREVIOUS_RELEASE_ID}/eclipse-platform-${props.PREVIOUS_RELEASE_VER}-linux-gtk-x86_64.tar.gz"
	return utilities.installDownloadableTool('eclipse', eclipseURL) + '/eclipse --launcher.suppressErrors -nosplash -consolelog'
}
