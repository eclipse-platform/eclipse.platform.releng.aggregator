pipeline {
	options {
		skipDefaultCheckout()
		timestamps()
		timeout(time: 120, unit: 'MINUTES')
		buildDiscarder(logRotator(numToKeepStr:'10'))
	}
	agent {
		label 'basic'
	}
	environment {
		HIDE_SITE = 'true'
		// Download Server locations (would very seldom change)
		EP_ROOT = '/home/data/httpd/download.eclipse.org'
	}
	tools {
		jdk 'temurin-jdk21-latest'
		maven 'apache-maven-latest'
	}
	stages {
		stage('Process input') {
			steps {
				script {
					env.DROP_ID = readParameter('DROP_ID')
					if (!"${DROP_ID}") {
						error("Parameter 'DROP_ID' is not specified")
					}
					env.CHECKPOINT = readParameter('CHECKPOINT')
					env.SIGNOFF_BUG = readParameter('SIGNOFF_BUG')
					def idMatcher = null
					if ((idMatcher = env.DROP_ID =~ /I(?<date>\d{8})-(?<time>\d{4})/).matches()) {
						assignEnvVariable('BUILD_LABEL', "${DROP_ID}")
					} else if ((idMatcher = env.DROP_ID =~ /S-(?<label>\d+\.\d+(\.\d+)?(M|RC)\d+[a-z]?)-(?<date>\d{8})(?<time>\d{4})/).matches()) {
						assignEnvVariable('BUILD_LABEL', idMatcher.group('label'))
						if ("${CHECKPOINT}") {
							error "Stable build DROP_ID=${DROP_ID} may only be promoted to release CHECKPOINT, which therefore must be empty: ${CHECKPOINT}"
						}
					} else {
						error "DROP_ID, ${DROP_ID}, did not match any expected pattern."
					}
					def buildTimestamp = idMatcher.group('date') + idMatcher.group('time')
					assignEnvVariable('REPO_ID', "I${idMatcher.group('date')}-${idMatcher.group('time')}")
					idMatcher = null // release matcher as it's not serializable
					
					sh 'curl -o buildproperties.shsource --fail https://download.eclipse.org/eclipse/downloads/drops4/${DROP_ID}/buildproperties.shsource'
					def STREAM = sh(returnStdout: true, script: 'source ./buildproperties.shsource && echo ${STREAM}').trim()
					def versionMatcher = STREAM =~ /(?<major>\d+)\.(?<minor>\d+).(?<service>\d+)/
					if (!versionMatcher.matches()) {
						error "STREAM must contain major, minor, and service versions, such as '4.37.0', but found: ${STREAM}"
					}
					assignEnvVariable('BUILD_MAJOR', versionMatcher.group('major'))
					assignEnvVariable('BUILD_MINOR', versionMatcher.group('minor'))
					assignEnvVariable('BUILD_SERVICE', versionMatcher.group('service'))
					versionMatcher = null // release matcher as it's not serializable
					
					if ("${CHECKPOINT}" ==~ /M\d+([a-z])?/ || "${CHECKPOINT}" ==~ /RC\d+([a-z])?/) { // milestone or RC promotion
						assignEnvVariable('DL_TYPE', 'S')
					} else if (!"${CHECKPOINT}") { // release promotion
						assignEnvVariable('DL_TYPE', 'R')
					} else {
						error "CHECKPOINT, ${CHECKPOINT}, did not match any expected pattern."
					}
					
					assignEnvVariable('DL_LABEL', "${BUILD_SERVICE}" == '0' // For initial releases, do not include service in label
						? "${BUILD_MAJOR}.${BUILD_MINOR}${CHECKPOINT}"
						: "${BUILD_MAJOR}.${BUILD_MINOR}.${BUILD_SERVICE}${CHECKPOINT}"
					)
					// This is DL_DROP_ID for Eclipse and Equinox
					assignEnvVariable('DL_DROP_ID', "${DL_TYPE}-${DL_LABEL}-${buildTimestamp}")
					assignEnvVariable('MAINTENANCE_BRANCH', "R${BUILD_MAJOR}_${BUILD_MINOR}_maintenance")
					
					if (!env.SIGNOFF_BUG) {
						echo '''\
							[WARNING] SIGNOFF_BUG was not defined. That is valid if no Unit Tests failures but otherwise should be defined.
							Can be added by hand to buildproperties.php in drop site, if in fact there were errors, and simply forgot to specify.
						'''.stripIndent()
					} else if (env.SIGNOFF_BUG ==~ '\\d+') {
						assignEnvVariable('SIGNOFF_BUG', "https://github.com/eclipse-platform/eclipse.platform.releng.aggregator/issues/${SIGNOFF_BUG}")
					}
				}
			}
		}
		stage('Checkout SCM') {
			steps {
				dir("${WORKSPACE}/repository") {
					checkout scm
					script { // Always load the script from the very same state this pipeline is loaded (to ensure consistency)
						utilities = load "JenkinsJobs/shared/utilities.groovy"
						githubAPI = load "JenkinsJobs/shared/githubAPI.groovy"
					}
					sh '''#!/bin/bash -xe
						git branch --force master HEAD
						git fetch origin "refs/heads/${MAINTENANCE_BRANCH}"
						git reflog show master
						git reflog show "origin/${MAINTENANCE_BRANCH}"
						
						git fetch origin tag "${REPO_ID}"
						git checkout "${REPO_ID}"
						# Check out all submodules at the specified REPO_ID
						git submodule update --init --recursive --depth 1
						
						git config --global user.email 'releng-bot@eclipse.org'
						git config --global user.name 'Eclipse Releng Bot'
					'''
				}
			}
		}
		stage('Move and rename Pages') {
			steps {
				sshagent(['projects-storage.eclipse.org-bot-ssh']) {
					
					echo 'Promote Equinox'
					dir("${WORKSPACE}/equinox") {
						renameBuildDrop('equinox/drops', "${DROP_ID}", "${BUILD_LABEL}", "${DL_DROP_ID}", "${DL_LABEL}")
					}
					
					echo 'Promote Eclipse'
					dir("${WORKSPACE}/eclipse") {
						renameBuildDrop('eclipse/downloads/drops4', "${DROP_ID}", "${BUILD_LABEL}", "${DL_DROP_ID}", "${DL_LABEL}") {
							sh '''
								echo "\\$NEWS_ID = \\"${BUILD_MAJOR}.${BUILD_MINOR}\\";" >> 'buildproperties.php'
								echo "\\$ACK_ID = \\"${BUILD_MAJOR}.${BUILD_MINOR}\\";" >> 'buildproperties.php'
								echo "\\$README_ID = \\"${BUILD_MAJOR}.${BUILD_MINOR}\\";" >> 'buildproperties.php'
								
								# SIGNOFF_BUG should not be defined if there are no JUnit failures to investigate and explain
								if [[ -n "${SIGNOFF_BUG}" ]]; then
									echo -e "<p>Any unit test failures below have been investigated and found to be test-related and do not affect the quality of the build.\\nSee the <a href=\\"${SIGNOFF_BUG}\\">sign-off page</a> for details.</p>" > 'testNotes.html'
								fi
							'''
						}
					}
				}
			}
		}
		stage('Tag Build') {
			environment {
				TAG = "${DL_TYPE}${BUILD_MAJOR}_${BUILD_MINOR}${(CHECKPOINT || BUILD_SERVICE != '0') ? ('_' + BUILD_SERVICE) : ''}${CHECKPOINT ? ('_' + CHECKPOINT) : ''}"
			}
			steps {
				dir("${WORKSPACE}/repository") {
					sshagent(['github-bot-ssh']) {
						sh '''#!/bin/bash -xe
							function tagBuild() {
								pushURL=$(git config remote.origin.url)
								if [[ "$pushURL" == http* ]]; then
									# Change to SSH, if the configured URL uses HTTPS (we can only push with SSH)
									pushURL=$(echo $pushURL|sed --expression 's|https://github.com/|git@github.com:|')
								fi
								git tag -a -m "${SIGNOFF_BUG}" ${TAG} HEAD
								# git push fails if the tag already exists
								git push --verbose ${pushURL} tag ${TAG}
							}
							tagBuild
							export -f tagBuild
							git submodule foreach 'tagBuild'
						'''
					}
				}
			}
		}
		stage('Promote P2 Repository') {
			when {
				environment name: 'DL_TYPE', value: 'R'
			}
			steps {
				dir("${WORKSPACE}/updates") {
					sshagent(['projects-storage.eclipse.org-bot-ssh']) {
						sh '''#!/bin/bash -xe
							sourceRepo="eclipse/updates/${BUILD_MAJOR}.${BUILD_MINOR}-I-builds/${REPO_ID}"
							targetRepo="eclipse/updates/${BUILD_MAJOR}.${BUILD_MINOR}/${DL_DROP_ID}"
							ssh genie.releng@projects-storage.eclipse.org cp -r "${EP_ROOT}/${sourceRepo}/." "${EP_ROOT}/${targetRepo}"
							
							MIRRORS_URL="https://www.eclipse.org/downloads/download.php?file=/${targetRepo}"
							mvn tycho-p2-repository:modify-repository-properties -Pp2-repository-modification \\
								--file ${WORKSPACE}/repository/eclipse-platform-parent/pom.xml \\
								-Dp2.repository.location=https://download.eclipse.org/${sourceRepo}/ \\
								-Dp2.repository.output=$(pwd)/output \\
								-Dp2.repository.kind=artifact \\
								-Dp2.repository.mirrorsURL=${MIRRORS_URL}
							
							scp output/artifacts.* genie.releng@projects-storage.eclipse.org:${EP_ROOT}/${targetRepo}
						'''
					}
				}
			}
		}
		stage('Update build configurations') {
			when {
				environment name: 'DL_TYPE', value: 'R'
			}
			environment {
				RELEASE_P2_REPOSITORY = "https://download.eclipse.org/eclipse/updates/${BUILD_MAJOR}.${BUILD_MINOR}/${DL_DROP_ID}/"
			}
			steps {
				dir("${WORKSPACE}/repository") {
					script { // Update the master branch
						sh 'git checkout master'
						sh '''
							mvn -f eclipse-platform-parent/pom.xml org.eclipse.tycho:tycho-versions-plugin:set-property \
								-Dproperties=previous-release.baseline \
								-DnewPrevious-release.baseline="${RELEASE_P2_REPOSITORY}"
						'''
						utilities.replaceAllInFile('cje-production/buildproperties.txt', [
							'BASEBUILDER_TAG=".*?"' : "BASEBUILDER_TAG=\"${BUILD_MAJOR}.${BUILD_MINOR}\"",
							'API_PREV_REF_LABEL=".*?"' : "API_PREV_REF_LABEL=\"${BUILD_MAJOR}.${BUILD_MINOR}\"",
							'PREVIOUS_RELEASE_VER=".*?"' : "PREVIOUS_RELEASE_VER=\"${BUILD_MAJOR}.${BUILD_MINOR}\"",
							'PREVIOUS_RELEASE_REPO_ID=".*?"' : "PREVIOUS_RELEASE_REPO_ID=\"${BUILD_MAJOR}.${BUILD_MINOR}\"",
							'BASEBUILD_ID=".*?"' : "BASEBUILD_ID=\"${DL_DROP_ID}\"",
							'PREVIOUS_RELEASE_ID=".*?"' : "PREVIOUS_RELEASE_ID=\"${DL_DROP_ID}\"",
						])
						utilities.replaceAllInFile('eclipse.platform.releng.tychoeclipsebuilder/eclipse-junit-tests/src/main/resources/equinoxp2tests.properties', [
							'eclipse-platform-\\d+.\\d+[^-]*?-' : "eclipse-platform-${BUILD_MAJOR}.${BUILD_MINOR}-",
							'org.eclipse.equinox.p2.tests.last.release.build.repo=.*' : "org.eclipse.equinox.p2.tests.last.release.build.repo=https://download.eclipse.org/equinox/drops/${DL_DROP_ID}/",
						])
						utilities.replaceAllInFile('eclipse.platform.releng.tychoeclipsebuilder/eclipse-junit-tests/src/main/resources/label.properties', [
							'previousReleaseVersion=.*' : "previousReleaseVersion=${BUILD_MAJOR}.${BUILD_MINOR}",
						])
						utilities.replaceAllInFile('eclipse.platform.releng.tychoeclipsebuilder/eclipse-junit-tests/src/main/scripts/getPreviousRelease.sh', [
							'R\\/R-\\d+.\\d+-\\d{12}' : "R/${DL_DROP_ID}",
							'-\\d+.\\d+-linux-gtk-' : "-${BUILD_MAJOR}.${BUILD_MINOR}-linux-gtk-",
						])
						utilities.replaceAllInFile('production/testScripts/configuration/streamSpecific.properties', [
							'previousReleaseLocation=.*' : 'previousReleaseLocation=https://\\${DOWNLOAD_HOST}/eclipse/downloads/drops4/' + DL_DROP_ID + '/',
							'previousReleaseVersion=.*' : "previousReleaseVersion=${BUILD_MAJOR}.${BUILD_MINOR}",
							'previousReleaseVersionRepo=.*' : "previousReleaseVersionRepo=${BUILD_MAJOR}.${BUILD_MINOR}",
						])
						
						utilities.commitAllChangesExcludingSubmodules("Update previous release version to ${BUILD_MAJOR}.${BUILD_MINOR} GA across build scripts")
					}
					script { // Update the maintenance branch
						sh 'git checkout -b updateMaintenance "origin/${MAINTENANCE_BRANCH}"'
						def ecjManifest = sh(script: "curl https://raw.githubusercontent.com/eclipse-jdt/eclipse.jdt.core/refs/tags/${REPO_ID}/org.eclipse.jdt.core.compiler.batch/META-INF/MANIFEST.MF", returnStdout: true).trim()
						def bundleVersion = readManifest(text: ecjManifest).main['Bundle-Version']
						def ecjVersion = bundleVersion.substring(0, bundleVersion.indexOf('.qualifier'))
						sh """
							mvn -f eclipse-platform-parent/pom.xml org.eclipse.tycho:tycho-versions-plugin:set-property \
								-Dproperties=eclipse-sdk-repo,previous-release.baseline,cbi-ecj-version \
								-DnewEclipse-sdk-repo="${RELEASE_P2_REPOSITORY}" \
								-DnewPrevious-release.baseline="${RELEASE_P2_REPOSITORY}" \
								-DnewCbi-ecj-version="${ecjVersion}"
						"""
						utilities.replaceAllInFile('cje-production/buildproperties.txt', [
							'ECLIPSE_RUN_REPO="https://download.eclipse.org/eclipse/updates/.*"' : "ECLIPSE_RUN_REPO=\"${RELEASE_P2_REPOSITORY}\"",
						])
						
						utilities.commitAllChangesExcludingSubmodules("Update ${MAINTENANCE_BRANCH} branch with release version for ${BUILD_MAJOR}_${BUILD_MINOR}+ changes")
					}
					// Switch back to master for subsequent parts of this pipeline
					sh 'git checkout master'
					
					// Display created commits
					sh '''#!/bin/bash -xe
						git log origin/master..master --patch-with-stat --summary
						git log origin/${MAINTENANCE_BRANCH}..updateMaintenance --patch-with-stat --summary
					'''
					sshagent(['github-bot-ssh']) {
						sh '''#!/bin/bash -xe
							pushURL=$(git config remote.origin.url)
							# Switch to SSH, if the configured URL uses HTTPS (we can only push with SSH)
							if [[ "$pushURL" == http* ]]; then
								pushURL=$(echo $pushURL|sed --expression 's|https://github.com/|git@github.com:|')
							fi
							
							git push ${pushURL} "master:refs/heads/update-build-to-R${BUILD_MAJOR}.${BUILD_MINOR}"
							git push ${pushURL} "updateMaintenance:refs/heads/update-${MAINTENANCE_BRANCH}"
						'''
					}
					// Create PRs agains the master and maintenance branch
					script { withEnv(['GITHUB_BOT_TOKEN=' + credentials('github-bot-token')]) {
						def masterPR = githubAPI.createPullRequest('eclipse-platform/eclipse.platform.releng.aggregator',
							"Update previous release version to ${BUILD_MAJOR}.${BUILD_MINOR} GA across build scripts", """\
							Update the the `${MAINTENANCE_BRANCH}` branch with final ${BUILD_MAJOR}.${BUILD_MINOR} release version.
							
							**This should not be submitted before ${BUILD_MAJOR}.${BUILD_MINOR} is finally released.**
							""".stripIndent(),"update-build-to-R${BUILD_MAJOR}.${BUILD_MINOR}", 'master')
						
						githubAPI.createPullRequest('eclipse-platform/eclipse.platform.releng.aggregator',
							"Update ${MAINTENANCE_BRANCH} branch with release version for ${BUILD_MAJOR}_${BUILD_MINOR}+ changes", """\
							Update the the `${MAINTENANCE_BRANCH}` branch with final ${BUILD_MAJOR}.${BUILD_MINOR} release version.
							This complements:
							- ${masterPR}
							
							**This should not be submitted before ${BUILD_MAJOR}.${BUILD_MINOR} is finally released.**
							""".stripIndent(),"update-${MAINTENANCE_BRANCH}", "${MAINTENANCE_BRANCH}")
					}}
				}
			}
		}
		stage('Stage to Maven-Central') {
			when {
				environment name: 'DL_TYPE', value: 'R'
			}
			steps {
				build job: 'Releng/deployToMaven', wait: true, propagate: true, parameters: [
					string(name: 'sourceRepository', value: "https://download.eclipse.org/eclipse/updates/${BUILD_MAJOR}.${BUILD_MINOR}/${DL_DROP_ID}/")
				]
			}
		}
		stage('Update acknowledgements') {
			environment {
				GITHUB_BOT_TOKEN = credentials('github-bot-token')
			}
			steps {
				script {
					githubAPI.triggerWorkflow('eclipse-platform/www.eclipse.org-eclipse', 'generateAcknowledgements.yml', [ 'eclipse-version': "${BUILD_MAJOR}.${BUILD_MINOR}" ])
				}
			}
		}
		stage('Trigger publication') {
			when {
				not {
					environment name: 'DL_TYPE', value: 'R'
				}
			}
			steps {
				build job: 'Releng/publishPromotedBuild', wait: true, propagate: true, parameters: [
					string(name: 'releaseBuildID', value: "${DL_DROP_ID}")
				]
			}
		}
	}
	post {
		always {
			archiveArtifacts artifacts: '**/*', excludes: 'repository/**'
		}
	}
}

def renameBuildDrop(String baseDropPath, String oldDropID, String oldBuildLabel, String newDropID, String newBuildLabel, Closure extraTasks=null) {
	def sourcePath="${EP_ROOT}/${baseDropPath}/${oldDropID}"
	def targetPath="${EP_ROOT}/${baseDropPath}/${newDropID}"
	
	sh """#!/bin/bash -xe
		
		# Copy drop-directory to new location
		ssh genie.releng@projects-storage.eclipse.org mkdir -p '${targetPath}'
		if [[ "${HIDE_SITE}" == "true" ]]; then
			# Create this marker first to ensure the page is never without
			ssh genie.releng@projects-storage.eclipse.org touch '${targetPath}/buildHidden'
		fi
		ssh genie.releng@projects-storage.eclipse.org cp -r '${sourcePath}/.' '${targetPath}'
		
		# Rename files
		echo 'Rename files at new location from *${oldBuildLabel}* to *${newBuildLabel}*.'
		# References to locally defined variables required double escaping
		# because they are escaped for groovy and the bash HERE document.
		# In general this section is very fragile! Only change with great caution and extensive testing!
		ssh genie.releng@projects-storage.eclipse.org << EOF
			set -xe
			cd "${targetPath}"
			files=\\\$(find . -mindepth 1 -maxdepth 2 -name "*${oldBuildLabel}*" -print)
			echo " \\\$(echo "\\\$files" | wc -l) files found to rename."
			
			for file in \\\${files}; do
				if [[ \\\$file =~ (.*)(${oldBuildLabel})(.*) ]]; then
					mv "\\\$file" "\\\${BASH_REMATCH[1]}${newBuildLabel}\\\${BASH_REMATCH[3]}"
				fi
			done
#DO NOT INDENT EOF!
EOF
		
		#Update checksums to new filenames
		ssh genie.releng@projects-storage.eclipse.org sed --in-place --expression 's/${oldBuildLabel}/${newBuildLabel}/g' ${targetPath}/checksum/*
	"""
	
	// Update buildproperties.php to new names
	def newTypeName = null
	if ("${DL_TYPE}" == 'R') {
		newTypeName = 'Release'
	} else if (newBuildLabel.contains('RC')) {
		newTypeName = 'Release Candidate'
	} else if ("${DL_TYPE}" == 'S') {
		newTypeName = 'Stable'
	} else {
		error "Unexpected DL_TYPE value, ${DL_TYPE}"
	}
	def newProperties = [
		BUILD_ID: "${newBuildLabel}",
		BUILD_TYPE: "${DL_TYPE}",
		BUILD_TYPE_NAME: "${newTypeName}",
		BUILD_DIR_SEG: "${newDropID}",
		EQ_BUILD_DIR_SEG: "${newDropID}",
	]
	def buildPropertiesPHP = sh(returnStdout: true, script: "ssh genie.releng@projects-storage.eclipse.org cat ${sourcePath}/buildproperties.php")
	for (entry in newProperties) {
		buildPropertiesPHP = buildPropertiesPHP.replaceFirst('\\$' + entry.key + ' = "[^"]+"', '\\$' + entry.key + ' = "' + entry.value + '"')
	}
	writeFile(file: 'buildproperties.php', text: buildPropertiesPHP)
	
	if (extraTasks) {
		extraTasks()
	}
	sh """#!/bin/bash -xe
		# Copy locally modified files to the download server
		scp -r . genie.releng@projects-storage.eclipse.org:${targetPath}/
		
		if [[ '${DL_TYPE}' == 'R' ]]; then
			echo 'Creating archive'
			ssh genie.releng@projects-storage.eclipse.org cp -r '${targetPath}' /home/data/httpd/archive.eclipse.org/${baseDropPath}/
		fi
	"""
}

@groovy.transform.Field
def utilities = null
@groovy.transform.Field
def githubAPI = null

@NonCPS
def readParameter(String name) {
	//TODO: let jenkins trim the parameters
	def value = (params[name] ?: '').trim()
	println("${name}: ${value}")
	return value
}

@NonCPS
def assignEnvVariable(String name, String value) {
	env."${name}" = value
	println("${name}=${value}")
}
