// Constants read by job creation
private static final _JOB_DESCRIPTION = 'Add or remove children from an Eclipse-P2 composite repository.'

pipeline {
	options {
		disableConcurrentBuilds() // prevent concurrent updates of the same repository
		timestamps()
		skipDefaultCheckout()
		timeout(time: 15, unit: 'MINUTES')
		buildDiscarder(logRotator(numToKeepStr:'5'))
	}
	parameters {
		string(name: 'repositoryPath', trim: true, description: 'Relative repository path from https://download.eclipse.org/. E.g.: eclipse/updates/4.37-I-builds')
		string(name: 'add', trim: true, description: 'Comma separated list of children to add. May be empty')
		string(name: 'remove', trim: true, description: 'Comma separated list of children to remove. May be empty')
		string(name: 'sizeLimit', trim: true, description: '''\
			The maximum number of childrem the modified composite should contain.
			If the total number of children exceeds this limit (after adding new ones), a corresponding number of children is removed from the beginning of the list.
		''')
		string(name: 'repositoryName', trim: true, description: 'Optional name attribute of the composite repository to set (if blank the name is not changed)')
	}
	agent {
		label 'basic'
	}
	tools {
		jdk 'temurin-jdk21-latest'
		maven 'apache-maven-latest'
	}
	stages {
		stage('Update composite repository') {
			environment {
				OUTPUT_PATH = 'output-repository'
			}
			steps {
				sh """
					git clone --depth=1 --filter=tree:0 --no-checkout --branch=${scm.branches[0].name} ${scm.userRemoteConfigs[0].url} repository
					pushd repository
					git sparse-checkout set --no-cone eclipse-platform-parent/pom.xml
					git checkout
					popd
					
					mvn tycho-p2-repository:modify-composite-repository -Pp2-repository-modification \\
						--file ${WORKSPACE}/repository/eclipse-platform-parent/pom.xml \\
						-Dp2.repository.location=https://download.eclipse.org/${params.repositoryPath}/ \\
						-Dp2.repository.output=${WORKSPACE}/${OUTPUT_PATH} \\
						-Dp2.composite.children.add=${params.add} \\
						-Dp2.composite.children.remove=${params.remove} \\
						-Dp2.composite.children.limit=${params.sizeLimit ?: 0} \\
						-Dp2.composite.validate=false \\
						${params.repositoryName ? ("-Dp2.repository.name='" + params.repositoryName + "'") : ''} \\
				"""
					sshagent(['projects-storage.eclipse.org-bot-ssh']) {
						sh '''
							epDownloadsDir='/home/data/httpd/download.eclipse.org'
							ssh genie.releng@projects-storage.eclipse.org mkdir -p "${epDownloadsDir}/${repositoryPath}"
							scp -r ${OUTPUT_PATH}/* "genie.releng@projects-storage.eclipse.org:${epDownloadsDir}/${repositoryPath}"
						'''
				}
			}
		}
	}
	post {
		always {
			archiveArtifacts allowEmptyArchive: true, artifacts: '**/*'
		}
	}
}
