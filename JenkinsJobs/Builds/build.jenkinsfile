
def I_TEST_CONFIGURATIONS = [
  [ os: 'linux' , ws: 'gtk'  , arch: 'x86_64' , javaVersion: 21],
  [ os: 'linux' , ws: 'gtk'  , arch: 'x86_64' , javaVersion: 23],
  [ os: 'macosx', ws: 'cocoa', arch: 'aarch64', javaVersion: 21],
  [ os: 'macosx', ws: 'cocoa', arch: 'x86_64' , javaVersion: 21],
  [ os: 'win32' , ws: 'win32', arch: 'x86_64' , javaVersion: 21],
]
def Y_TEST_CONFIGURATIONS = [
  [ os: 'linux' , ws: 'gtk'  , arch: 'x86_64' , javaVersion: 21],
  [ os: 'linux' , ws: 'gtk'  , arch: 'x86_64' , javaVersion: 24],
  [ os: 'macosx', ws: 'cocoa', arch: 'aarch64', javaVersion: 21],
  [ os: 'macosx', ws: 'cocoa', arch: 'x86_64' , javaVersion: 21],
]
def BUILD = {
	def matcher = "$JOB_BASE_NAME" =~ '(?<type>[IY])-build-(?<major>\\d).(?<minor>\\d+)'
	if (matcher) {
		def buildConfig = [ type: matcher.group('type'), testPrefix: "ep${matcher.group('major')}${matcher.group('minor')}${matcher.group('type')}-unit"]
		switch(buildConfig.type) {
			case 'I': return [*:buildConfig,
				typeName: 'Integration' , branchLabel: 'master',
				mailingList: 'platform-releng-dev@eclipse.org', testJobFolder:'AutomatedTests', testConfigurations: I_TEST_CONFIGURATIONS]
			case 'Y': return [*:buildConfig,
				typeName: 'Beta Java 24', branchLabel: 'java24',
				mailingList: 'jdt-dev@eclipse.org'            , testJobFolder:'YPBuilds'      , testConfigurations: Y_TEST_CONFIGURATIONS]
		}
	}
	throw new IllegalStateException("Unsupported job: $JOB_BASE_NAME" )
}()

def testConfigurationsExpected = BUILD.testConfigurations.collect{c ->
		"${BUILD.testPrefix}-${c.os}-${c.arch}-java${c.javaVersion}_${c.os}.${c.ws}.${c.arch}_${c.javaVersion}"
	}.join(',')

pipeline {
	options {
		timeout(time: 360, unit: 'MINUTES')
		timestamps()
		buildDiscarder(logRotator(numToKeepStr:'25'))
		skipDefaultCheckout()
	}
  agent {
    kubernetes {
      inheritFrom 'ubuntu-2404'
      yaml """
apiVersion: v1
kind: Pod
spec:
  containers:
  - name: "jnlp"
    resources:
      limits:
        memory: "10Gi"
        cpu: "4000m"
      requests:
        memory: "6144Mi"
        cpu: "2000m"
"""
    }
  }
	tools {
		jdk 'temurin-jdk21-latest'
		maven 'apache-maven-latest'
		ant 'apache-ant-latest'
	}
	environment {
		BUILD_TYPE = "${BUILD.type}"
		BUILD_TYPE_NAME = "${BUILD.typeName}"
		PATCH_OR_BRANCH_LABEL = "${BUILD.branchLabel}"

		MAVEN_OPTS = "-Xmx6G"
		CJE_ROOT = "${WORKSPACE}/eclipse.platform.releng.aggregator/eclipse.platform.releng.aggregator/cje-production"
		logDir = "$CJE_ROOT/buildlogs"
		TEST_CONFIGURATIONS_EXPECTED = "${testConfigurationsExpected}"
	}
	stages {
	  stage('Setup intial configuration'){
          steps {
                  sshagent(['github-bot-ssh']) {
                      dir ('eclipse.platform.releng.aggregator') {
                        sh '''
                            git clone -b master git@github.com:eclipse-platform/eclipse.platform.releng.aggregator.git
                        '''
                      }
                    }
				dir("${CJE_ROOT}") {
                    sh '''
                        chmod +x mbscripts/*
                        mkdir -p $logDir
                    '''
				}
			}
		}
	  stage('Generate environment variables'){
          steps {
				dir("${CJE_ROOT}/mbscripts") {
                sh '''
                    ./mb010_createEnvfiles.sh $CJE_ROOT/buildproperties.shsource 2>&1 | tee $logDir/mb010_createEnvfiles.sh.log
                    if [[ ${PIPESTATUS[0]} -ne 0 ]]
                    then
                        echo "Failed in Generate environment variables stage"
                        exit 1
                    fi
                '''
				}
			}
		}
	  stage('Load PGP keys'){
          environment {
                KEYRING = credentials('secret-subkeys-releng.asc')
                KEYRING_PASSPHRASE = credentials('secret-subkeys-releng.acs-passphrase')
          }
          steps {
				dir("${CJE_ROOT}/mbscripts") {
                sh '''
                    ./mb011_loadPGPKeys.sh 2>&1 | tee $logDir/mb011_loadPGPKeys.sh.log
                    if [[ ${PIPESTATUS[0]} -ne 0 ]]
                    then
                        echo "Failed in Load PGP keys"
                        exit 1
                    fi
                '''
				}
			}
		}
		stage('Export environment variables stage 1'){
			steps {
				script {
					def buildProps = readBuildProperties("${CJE_ROOT}/buildproperties.properties")
					env.BUILD_IID = buildProps.BUILD_TYPE + buildProps.TIMESTAMP
					env.STREAM = buildProps.STREAM
					env.RELEASE_VER = buildProps.RELEASE_VER
				}
			}
		}
	  stage('Create Base builder'){
          steps {
				dir("${CJE_ROOT}/mbscripts") {
		      sshagent(['projects-storage.eclipse.org-bot-ssh']) {
		                sh '''
		                    ./mb020_createBaseBuilder.sh $CJE_ROOT/buildproperties.shsource 2>&1 | tee $logDir/mb020_createBaseBuilder.sh.log
		                    if [[ ${PIPESTATUS[0]} -ne 0 ]]
		                    then
		                        echo "Failed in Create Base builder stage"
		                        exit 1
		                    fi
		                '''
		        }
				}
			}
		}
	  stage('Download reference repo for repo reports'){
          steps {
				dir("${CJE_ROOT}/mbscripts") {
                  sshagent(['projects-storage.eclipse.org-bot-ssh']) {
                    sh '''
                        ./mb030_downloadBuildToCompare.sh $CJE_ROOT/buildproperties.shsource 2>&1 | tee $logDir/mb030_downloadBuildToCompare.sh.log
                        if [[ ${PIPESTATUS[0]} -ne 0 ]]
                        then
                            echo "Failed in Download reference repo for repo reports stage"
                            exit 1
                        fi
                    '''
                  }
				}
			}
		}
	  stage('Clone Repositories'){
          steps {
				dir("${CJE_ROOT}/mbscripts") {
                  sshagent(['git.eclipse.org-bot-ssh', 'github-bot-ssh']) {
                    sh '''
                        git config --global user.email "eclipse-releng-bot@eclipse.org"
                        git config --global user.name "Eclipse Releng Bot"
                        ./mb100_cloneRepos.sh $CJE_ROOT/buildproperties.shsource 2>&1 | tee $logDir/mb100_cloneRepos.sh.log
                        if [[ ${PIPESTATUS[0]} -ne 0 ]]
                        then
                            echo "Failed in Clone Repositories stage"
                            exit 1
                        fi
                    '''
                  }
				}
			}
		}
	  stage('Tag Build Inputs'){
          environment {
            ABORT_IF_NO_CHANGES = "${!currentBuild.getBuildCauses('hudson.triggers.TimerTrigger$TimerTriggerCause').isEmpty()}" // true, if triggered by timer
          }
          steps {
				dir("${CJE_ROOT}/mbscripts") {
                  sshagent (['git.eclipse.org-bot-ssh', 'github-bot-ssh', 'projects-storage.eclipse.org-bot-ssh']) {
                    sh '''
                        bash -x ./mb110_tagBuildInputs.sh $CJE_ROOT/buildproperties.shsource 2>&1 | tee $logDir/mb110_tagBuildInputs.sh.log
                        if [[ ${PIPESTATUS[0]} -ne 0 ]]
                        then
                            echo "Failed in Tag Build Inputs stage"
                            exit 1
                        fi
                    '''
                  }
				}
                  script {
                    if (env.ABORT_IF_NO_CHANGES && fileExists("${WORKSPACE}/noChanges")) {
						emailext subject: "${RELEASE_VER} ${BUILD_TYPE}-Build: ${BUILD_IID} - BUILD SKIPPED",
							body: """\
							No changes have been made since the last successful ${BUILD_TYPE}-Build and therefore this scheduled build was skipped:
							${BUILD_URL}console
							""".stripIndent(), mimeType: 'text/plain',
							to: "${BUILD.mailingList}", from: 'genie.releng@eclipse.org'
                      currentBuild.result = 'ABORTED'
                      error('Abort scheduled build due to no changes')
                    }
                  }
            }
		}
	  stage('Aggregator maven build'){
	      environment {
                KEYRING = credentials('secret-subkeys-releng.asc')
                MAVEN_GPG_PASSPHRASE = credentials('secret-subkeys-releng.acs-passphrase')
          }
          steps {
				dir("${CJE_ROOT}/mbscripts") {
                    sh '''
                        unset JAVA_TOOL_OPTIONS 
                        unset _JAVA_OPTIONS
                        ./mb220_buildSdkPatch.sh $CJE_ROOT/buildproperties.shsource 2>&1 | tee $logDir/mb220_buildSdkPatch.sh.log
                        if [[ ${PIPESTATUS[0]} -ne 0 ]]
                        then
                            echo "Failed in Aggregator maven build stage"
                            exit 1
                        fi
                    '''
				}
			}
		}
	  stage('Gather Eclipse Parts'){
	      environment {
                KEYRING = credentials('secret-subkeys-releng.asc')
                KEYRING_PASSPHRASE = credentials('secret-subkeys-releng.acs-passphrase')
          }
          steps {
				dir("${CJE_ROOT}/mbscripts") {
                          sh '''
                            bash -x ./mb300_gatherEclipseParts.sh $CJE_ROOT/buildproperties.shsource 2>&1 | tee $logDir/mb300_gatherEclipseParts.sh.log
                            if [[ ${PIPESTATUS[0]} -ne 0 ]]
                            then
                                echo "Failed in Gather Eclipse Parts stage"
                                exit 1
                            fi
                          '''
				}
			}
		}
	  stage('Gather Equinox Parts'){
	  environment {
                KEYRING = credentials('secret-subkeys-releng.asc')
                KEYRING_PASSPHRASE = credentials('secret-subkeys-releng.acs-passphrase')
          }
          steps {
				dir("${CJE_ROOT}/mbscripts") {
                          sh '''
                            ./mb310_gatherEquinoxParts.sh $CJE_ROOT/buildproperties.shsource 2>&1 | tee $logDir/mb310_gatherEquinoxParts.sh.log
                            if [[ ${PIPESTATUS[0]} -ne 0 ]]
                            then
                                echo "Failed in Gather Equinox Parts stage"
                                exit 1
                            fi
                          '''
				}
			}
		}
	  stage('Generate Repo reports'){
          steps {
				dir("${CJE_ROOT}/mbscripts") {
                      sh '''
                        unset JAVA_TOOL_OPTIONS 
                        unset _JAVA_OPTIONS
                        ./mb500_createRepoReports.sh $CJE_ROOT/buildproperties.shsource 2>&1 | tee $logDir/mb500_createRepoReports.sh.log
                        if [[ ${PIPESTATUS[0]} -ne 0 ]]
                        then
                            echo "Failed in Generate Repo reports stage"
                            exit 1
                        fi
                      '''
				}
			}
		}
	  stage('Generate API tools reports'){
          steps {
				dir("${CJE_ROOT}/mbscripts") {
                      sh '''
                        unset JAVA_TOOL_OPTIONS 
                        unset _JAVA_OPTIONS
                        ./mb510_createApiToolsReports.sh $CJE_ROOT/buildproperties.shsource 2>&1 | tee $logDir/mb510_createApiToolsReports.sh.log
                        if [[ ${PIPESTATUS[0]} -ne 0 ]]
                        then
                            echo "Failed in Generate API tools reports stage"
                            exit 1
                        fi
                      '''
				}
			}
		}
		stage('Export environment variables stage 2'){
			steps {
				script {
					def buildProps = readBuildProperties("${CJE_ROOT}/buildproperties.properties")
					env.COMPARATOR_ERRORS_SUBJECT = buildProps.COMPARATOR_ERRORS_SUBJECT
					env.COMPARATOR_ERRORS_BODY = buildProps.COMPARATOR_ERRORS_BODY
				}
			}
		}
	  stage('Archive artifacts'){
          steps {
                sh '''
                    source $CJE_ROOT/buildproperties.shsource
                    cp -r $logDir/* $CJE_ROOT/$DROP_DIR/$BUILD_ID/buildlogs
                    rm -rf $logDir
                    rm -rf $CJE_ROOT/$DROP_DIR/$BUILD_ID/apitoolingreference
                    cp $CJE_ROOT/buildproperties.txt $CJE_ROOT/$DROP_DIR/$BUILD_ID
                    cp $CJE_ROOT/buildproperties.php $CJE_ROOT/$DROP_DIR/$BUILD_ID
                    cp $CJE_ROOT/buildproperties.properties $CJE_ROOT/$DROP_DIR/$BUILD_ID
                    cp $CJE_ROOT/buildproperties.shsource $CJE_ROOT/$DROP_DIR/$BUILD_ID
                    cp $CJE_ROOT/$DROP_DIR/$BUILD_ID/buildproperties.* $CJE_ROOT/$EQUINOX_DROP_DIR/$BUILD_ID
                '''
              archiveArtifacts '**/siteDir/**'
            }
		}
	  stage('Promote Eclipse platform'){
          steps {
				dir("${CJE_ROOT}/mbscripts") {
                  sshagent(['projects-storage.eclipse.org-bot-ssh']) {
                      sh '''
                        ./mb600_promoteEclipse.sh $CJE_ROOT/buildproperties.shsource
                      '''
                  }
                build job: 'eclipse.releng.updateIndex', wait: false
				}
			}
		}
	  stage('Promote Equinox'){
          steps {
				dir("${CJE_ROOT}/mbscripts") {
                  sshagent(['projects-storage.eclipse.org-bot-ssh']) {
                      sh '''
                        ./mb610_promoteEquinox.sh $CJE_ROOT/buildproperties.shsource
                      '''
                  }
				}
			}
		}
	  stage('Promote Update Site'){
          steps {
				dir("${CJE_ROOT}/mbscripts") {
                  sshagent(['projects-storage.eclipse.org-bot-ssh']) {
                      sh '''
                        ./mb620_promoteUpdateSite.sh $CJE_ROOT/buildproperties.shsource
                      '''
                  }
				}
			}
		}
		stage('Trigger tests'){
			steps {
				script {
					for (c in BUILD.testConfigurations) {
						build job: "${BUILD.testJobFolder}/${BUILD.testPrefix}-${c.os}-${c.arch}-java${c.javaVersion}", parameters: [string(name: 'buildId', value: "${BUILD_IID}")], wait: false
					}
				}
				build job: 'SmokeTests/Start-smoke-tests', parameters: [string(name: 'buildId', value: "${BUILD_IID}")], wait: false
			}
		}
		stage('Trigger publication to Maven snapshots repo') {
			when {
				allOf {
					environment name: 'BUILD_TYPE', value: 'I'
					environment name: 'COMPARATOR_ERRORS_SUBJECT', value: ''
				// On comparator-erros, skip the deployment of snapshot version to the 'eclipse-snapshots' maven repository to prevent that ECJ snapshot
				// from being used in verification builds. Similar to how the p2-repository is not added to the I-build composite in that case.
				}
			}
			steps {
				build job: 'CBIaggregator', parameters: [string(name: 'snapshotOrRelease', value: '-snapshot')], wait: false
			}
		}
	}
	post {
		failure {
			emailext subject: "${RELEASE_VER} ${BUILD_TYPE}-Build: ${BUILD_IID} - BUILD FAILED",
				body: "Please go to ${BUILD_URL}console and check the build failure.", mimeType: 'text/plain',
				to: "${BUILD.mailingList}", from:'genie.releng@eclipse.org'
			archiveArtifacts "${CJE_ROOT}/siteDir/eclipse/downloads/drops4/${BUILD_IID}/gitLog.html, $CJE_ROOT/gitCache/eclipse.platform.releng.aggregator"
		}
		success {
			emailext subject: "${RELEASE_VER} ${BUILD_TYPE}-Build: ${BUILD_IID} ${COMPARATOR_ERRORS_SUBJECT}",
			body: """\
			Eclipse downloads:
			https://download.eclipse.org/eclipse/downloads/drops4/${BUILD_IID}
			
			Build logs and/or test results (eventually):
			https://download.eclipse.org/eclipse/downloads/drops4/${BUILD_IID}/testResults.php
			
			${COMPARATOR_ERRORS_BODY}Software site repository:
			https://download.eclipse.org/eclipse/updates/${RELEASE_VER}-${BUILD_TYPE}-builds
			
			Specific (simple) site repository:
			https://download.eclipse.org/eclipse/updates/${RELEASE_VER}-${BUILD_TYPE}-builds/${BUILD_IID}
			
			Equinox downloads:
			https://download.eclipse.org/equinox/drops/${BUILD_IID}
			""".stripIndent(), mimeType: 'text/plain',
			to: "${BUILD.mailingList}", from:'genie.releng@eclipse.org'
		}
	}
}

def readBuildProperties(String buildPropertiesFile){
	return readProperties(file: buildPropertiesFile, charset: 'UTF-8').collectEntries{n, v ->
			v = v.trim();
			return [n, (v.startsWith('"') && v.endsWith('"') ? v.substring(1, v.length() - 1) : v)]
		}
}
