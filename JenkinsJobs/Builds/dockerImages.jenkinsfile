// Constants read by 
private static final _JOB_DESCRIPTION = 'Build and publish custom Docker images'

pipeline {
	options {
		skipDefaultCheckout()
		timestamps()
		timeout(time: 2, unit: 'HOURS')
		buildDiscarder(logRotator(numToKeepStr:'5'))
		disableConcurrentBuilds(abortPrevious: true)
	}
	triggers {
		cron '@weekly'
	}
	agent {
		label 'docker-build'
	}
	stages {
		stage('Get Docker Files') {
			steps {
				cleanWs()
				sh '''
					git clone --depth=1 -b master https://github.com/eclipse-platform/eclipse.platform.releng.aggregator.git
					git clone --depth=1 -b master https://github.com/eclipse-jdt/eclipse.jdt.core
				'''
			}
		}
		stage('Build and push Docker Images') {
			steps {
				script {
					dir('eclipse.platform.releng.aggregator/cje-production/dockerfiles') {
						parallel(
							'debian-swtnative': { buildAndPushImage('debian/swtnativebuild', 'eclipse/platformreleng-debian-swtnativebuild:12') },
							'debian-swtgtk3native': { buildAndPushImage('debian/swtgtk3nativebuild', 'eclipse/platformreleng-debian-swtgtk3nativebuild:10') },
							'centos9-gtk4': { buildAndPushImage('centos-gtk4-mutter/9-gtk4', 'eclipse/platformreleng-centos-gtk4-mutter:9') },
							'opensuse-gtk3': { buildAndPushImage('opensuse-gtk3-metacity/16-gtk3', 'eclipse/platformreleng-opensuse-gtk3-metacity:16') },
						failFast: false)
					}
					dir('eclipse.jdt.core/org.eclipse.jdt.core/docker') {
						parallel(
							'jikespg': { buildAndPushImage('jikespg', 'eclipse/platformreleng-ubuntu-gtk3-metacity:jikespg') },
						failFast: false)
					}
				}
			}
		}
		stage('Clean Docker Images') {
			steps {
				sh '''
					docker images
					docker system prune -a -f
					docker images
				'''
			}
		}
	}
	post {
		unsuccessful {
			emailext body: "Container images used for testing eclipse platform - Build Unsuccessful. Please go to ${BUILD_URL}console and check the log",
			subject: "Container images - Build Unsuccessful", 
			to: "platform-releng-dev@eclipse.org",
			from:"genie.releng@eclipse.org"
		}
		always {
			cleanWs()
		}
	}
}

def buildAndPushImage(directory, imageId) {
	dir(directory) {
		sh "docker build --pull -t ${imageId} ."
		withDockerRegistry([credentialsId: 'docker.com-bot', url: '']) {
			sh "docker push ${imageId}"
		}
	}
}
