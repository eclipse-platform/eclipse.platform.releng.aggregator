// Constants read by 
private static final _JOB_DESCRIPTION = 'Mark a build as stable/unstable or to (not to) be kept indefinitely.'

pipeline {
	options {
		skipDefaultCheckout()
		timestamps()
		timeout(time: 5, unit: 'MINUTES')
		buildDiscarder(logRotator(numToKeepStr:'5'))
	}
	parameters {
		string(name: 'buildId', trim: true, description: 'ID of the build to be marked.')
		choice(name: 'markAs', choices: [ 'STABLE', 'UNSTABLE', 'RETAINED_INDEFINITELY', 'NOT_RETAINED' ],
			description: 'The kind of marker to apply to (respectively remove from) the specified build.')
		string(name: 'issueURL', trim: true,
			description: 'URL of the causing Github issue or PR (<em>only relevant if the build is marked as unstable<em>).')
	}
	agent {
		label 'basic'
	}
	stages {
		stage('Mark build'){
			environment {
				// Download Server locations (would very seldom change)
				EP_BUILD_DROP = "/home/data/httpd/download.eclipse.org/eclipse/downloads/drops4/${buildId}"
				RELEASE_VER = readBuildProperty('RELEASE_VER')
			}
			steps {
				sshagent(['projects-storage.eclipse.org-bot-ssh']) {
					sh '''#!/bin/bash -xe
						if [ -z "$buildId" ]; then
							echo "BuildId is empty! Exiting."
							exit 1
						fi
						if [ "$markAs" == 'UNSTABLE' ] && [ -z "$issueURL" ]; then
							echo "Required issueURL parameter is empty! Exiting."
							exit 1
						fi
						
						case ${markAs} in
							STABLE)
								#Remove unstable tag
								ssh genie.releng@projects-storage.eclipse.org rm -f ${EP_BUILD_DROP}/buildUnstable
							;;
							UNSTABLE)
								# Convert URL of GH issue or PR into: 'organization/repository#number'
								label=$(echo "${issueURL##'https://github.com/'}" | sed 's/\\/issues\\//#/g' | sed 's/\\/pull\\//#/g')
								#Add unstable tag
								echo "This build is marked unstable due to <a href='${issueURL}'>${label}</a>." > buildUnstable
								scp buildUnstable genie.releng@projects-storage.eclipse.org:${EP_BUILD_DROP}/buildUnstable
							;;
							RETAINED_INDEFINITELY)
								#Add keep tag
								ssh genie.releng@projects-storage.eclipse.org touch ${EP_BUILD_DROP}/buildKeep
							;;
							NOT_RETAINED)
								#Remove keep tag
								ssh genie.releng@projects-storage.eclipse.org rm -f ${EP_BUILD_DROP}/buildKeep
							;;
						esac
					'''
				}
				build job: 'Releng/updateIndex', wait: false
				script {
					if (params.markAs == 'STABLE' || params.markAs == 'UNSTABLE') {
						build job: 'Releng/modifyP2CompositeRepository', wait: true, propagate: true, parameters: [
							string(name: 'repositoryPath', value: "eclipse/updates/${RELEASE_VER}-I-builds"),
							string(name: "${params.markAs == 'STABLE' ? 'add' : 'remove'}", value: "${buildId}")
						]
					}
				}
			}
		}
	}
}

def readBuildProperty(String name) {
	def buildPropertiesURL = "https://download.eclipse.org/eclipse/downloads/drops4/${buildId}/buildproperties.properties"
	def buildProperties = readProperties(text: sh(script: "curl --fail ${buildPropertiesURL}", returnStdout: true))
	return buildProperties[name].replace('"','') // Remove surrounding quotes
}
